apply plugin: 'cpp'

model {
    repositories {
        lib(PrebuiltLibraries) {
            gtest {
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("/usr/lib/x86_64-linux-gnu/libgtest.a")
                }
            }
            fakeit {
                headers.srcDir "../../lib/fakeit/include"
                headers.srcDir "../../lib/fakeit/config/gtest"
            }
            mtrace_gtest {
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("../mtrace_gtest/build/binaries/mtrace_gtestStaticLibrary/libmtrace_gtest.a")
                }
            }
            mtrace_gtest_main {
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("../mtrace_gtest/build/binaries/mtrace_gtest_mainStaticLibrary/libmtrace_gtest_main.a")
                }
            }
        }
    }

    components {
        main(NativeLibrarySpec) {
            sources.cpp.exportedHeaders {
                srcDir "include/main/cpp"
            }
        }
        test(NativeExecutableSpec) {
            binaries.all {
                cppCompiler.args "-std=c++11", "-Werror", "-Wall", "-Wextra",
                        "-pedantic"
                linker.args "-pthread"
            }
            sources.cpp {
                lib library: 'main'
                lib library: 'mtrace_gtest_main', linkage: 'static'
                lib library: 'mtrace_gtest', linkage: 'static'
                lib library: 'gtest', linkage: 'static'
                lib library: 'fakeit', linkage: 'api'
            }
        }
    }
}

task checkHeaderGuards(type: com.janitovff.headerguardplugin.HeaderGuardTask)

task test(type: Exec, dependsOn: ['testExecutable', 'checkHeaderGuards']) {
    doFirst {
        def binary = binaries.getByName 'testExecutable'
        def executableFile = binary.getExecutableFile()

        commandLine executableFile.toString(), '--gtest_color=yes'
    }
}
